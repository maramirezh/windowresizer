option(PROJECT_USEVCPKG "Use vcpkg in this project" ON)
if(PROJECT_USEVCPKG)
	if (NOT DEFINED ENV{VCPKG_ROOT})
		message(STATUS "VCPKG_ROOT not defined, using local vcpkg setup.")
		set(VCPKG_ROOT "${CMAKE_BINARY_DIR}/vcpkg" CACHE STRING "")
		if(NOT EXISTS ${VCPKG_ROOT}/bootstrap-vcpkg.sh)
			message(STATUS "Cloning vcpkg...")
			execute_process(COMMAND git clone https://github.com/Microsoft/vcpkg.git)
		endif()

		if(NOT EXISTS ${VCPKG_ROOT}/bootstrap-vcpkg.sh)
			message(FATAL_ERROR "Error: Could not clone vcpkg!")
		endif()

		if(WIN32)
			set(VCPKG_EXEC ${VCPKG_ROOT}/vcpkg.exe)
			set(VCPKG_BOOTSTRAP ${VCPKG_ROOT}/bootstrap-vcpkg.bat)
		else()
			set(VCPKG_EXEC ${VCPKG_ROOT}/vcpkg)
			set(VCPKG_BOOTSTRAP ${VCPKG_ROOT}/bootstrap-vcpkg.sh)
		endif()

		if(NOT EXISTS ${VCPKG_EXEC})
			execute_process(COMMAND ${VCPKG_BOOTSTRAP} WORKING_DIRECTORY ${VCPKG_ROOT})
		endif()

		if(NOT EXISTS ${VCPKG_EXEC})
			message(FATAL_ERROR "Error: Could not bootstrap vcpkg")
		endif()
		set(CMAKE_TOOLCHAIN_FILE ${VCPKG_ROOT}/scripts/buildsystems/vcpkg.cmake CACHE STRING "Vcpkg toolchain file")
	else()
		message(STATUS "VCPKG_ROOT is defined, using global vcpkg setup.")
		set(CMAKE_TOOLCHAIN_FILE $ENV{VCPKG_ROOT}/scripts/buildsystems/vcpkg.cmake CACHE STRING "Vcpkg toolchain file")
	endif()
	if(NOT DEFINED VCPKG_BINARY_SOURCES)
        message(STATUS "VCPKG_BINARY_SOURCES not defined, setting default.")
        set(VCPKG_BINARY_SOURCES "files,$ENV{LOCALAPPDATA}/vcpkg/archives;clear" CACHE STRING "vcpkg binary sources")
    endif()
endif()
